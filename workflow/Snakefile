'''Workflow for the CAMP MAG QC module.'''


from contextlib import redirect_stderr
import glob
import os
#from os import makedirs
#from os.path import getsize, isdir, os.path.join
import pandas as pd
from utils import Workflow_Dirs, ingest_samples, add_bin_num, get_bin_nums, pair_mag_refs, parse_dnadiff, aggregate_quast # polymut_from_cmseq


# Load and/or make the working directory structure
dirs = Workflow_Dirs(config['work_dir'], 'mag_qc')


# Load sample names and input files 
SAMPLES = ingest_samples(config['samples'], dirs.TMP)


# Specify the location of any external resources and scripts
dirs_ext = config['ext'] # os.path.join(dirname(abspath(__file__)), 'ext')
dirs_scr = os.path.join(dirs_ext, 'scripts')


# --- Workflow output --- #


rule all:
    input:
        os.path.join(dirs.OUT, 'final_reports', 'complete.txt') 


# --- Workflow steps --- #


rule checkm2:
    input:
        os.path.join(dirs.TMP, '{sample}.out'),
    output:
        os.path.join(dirs.OUT, '0_checkm2', '{sample}', 'quality_report.tsv'),
        # n50_sz = os.path.join(dirs.OUT, '0_checkm2', '{sample}', 'storage/bin_stats_ext.tsv'),
    log:
        os.path.join(dirs.LOG, 'checkm', '{sample}.checkm2.out'), 
    conda:
        "checkm2"
        #os.path.join(config['env_yamls'], 'checkm2.yaml'),
    threads: config['checkm_threads'],
    resources:
        mem_mb = config['checkm_mem_mb'],
    params:
        extension ='fa',
        bin_dir = os.path.join(dirs.TMP, '{sample}'),
        out_dir = os.path.join(dirs.OUT, '0_checkm2', '{sample}'),
        checkm2_db = config['checkm2_db'],
        # tmp_0 = os.path.join(dirs.OUT, '0_checkm2', '{sample}', 'quality_report.tsv'),
        # tmp_1 = os.path.join(dirs.OUT, '0_checkm2', '{sample}', 'tmp_1.csv'),
        # tmp_2 = os.path.join(dirs.OUT, '0_checkm2', '{sample}', 'tmp_2.csv'),
    shell:
        """
        checkm2 predict --threads {threads} --input {params.bin_dir} --output-directory {params.out_dir} \
            -x {params.extension} --database_path {params.checkm2_db} --force > {log} 2>&1
        """


rule checkm_sh:
    input:
        os.path.join(dirs.TMP, '{sample}.out'),
    output:
        os.path.join(dirs.OUT, '1_checkm1', 'strain_het', '{sample}', 'report.tsv'),
    log:
        os.path.join(dirs.LOG, 'checkm', '{sample}.strain_het.out'), 
    conda:
        'checkm-genome',
    threads: config['checkm_threads'],
    resources:
        mem_mb = config['checkm_mem_mb'],
    params:
        ext ='fa',
        bin_dir = os.path.join(dirs.TMP, '{sample}'),
        out_dir = os.path.join(dirs.OUT, '1_checkm1', 'strain_het', '{sample}'),
        checkm1_db = config['checkm1_db'],
    shell:
        """
        checkm data setRoot {params.checkm1_db}
        checkm lineage_wf -t {threads} -x {params.ext} --tab_table \
            -f {output} {params.bin_dir} {params.out_dir} > {log} 2>&1
        """


rule index_mag_bam:
    input:
        os.path.join(dirs.TMP, '{sample}.bam'),
    output:
        os.path.join(dirs.TMP, '{sample}.bam.bai'),
    threads: config['checkm_threads'],
    resources: 
        mem_mb = config['checkm_mem_mb'],
    shell:
        """
        samtools index -@ {threads} {input}
        """


rule checkm_cov:
    input:
        os.path.join(dirs.TMP, '{sample}.out'),
        os.path.join(dirs.TMP, '{sample}.bam.bai'),
    output:
        os.path.join(dirs.OUT, '1_checkm1', 'mag_ra', '{sample}', 'report_raw.tsv'),
    log:
        os.path.join(dirs.LOG, 'checkm', '{sample}.mag_ra.out'), 
    conda:
        'checkm-genome',
    threads: config['checkm_threads'],
    resources:
        mem_mb = config['checkm_mem_mb'],
    params:
        ext ='fa',
        bin_dir = os.path.join(dirs.TMP, '{sample}'),
        out_dir = os.path.join(dirs.OUT, '1_checkm1', 'mag_ra', '{sample}'),
        bam = os.path.join(dirs.TMP, '{sample}.bam'),
        checkm1_db = config['checkm1_db'],
    shell:
        """
        rm -r {params.out_dir}
        checkm data setRoot {params.checkm1_db}
        checkm coverage -t {threads} -x {params.ext} {params.bin_dir} \
            {output} {params.bam} > {log} 2>&1
        """


rule aggregate_cov:
    input:
        os.path.join(dirs.OUT, '1_checkm1', 'mag_ra', '{sample}', 'report_raw.tsv'),
    output:
        os.path.join(dirs.OUT, '1_checkm1', 'mag_ra', '{sample}', 'report.csv'),
    params:
        calc_script = os.path.join(dirs_scr, 'calc_mag_ra.py'),
    shell:
        """
        python {params.calc_script} {input} {output}
        """


rule gunc:
    input:
        os.path.join(dirs.TMP, '{sample}.out'),
    output:
        os.path.join(dirs.OUT, '2_gunc', '{sample}', 'GUNC.progenomes_2.1.maxCSS_level.tsv'),
    log:
        os.path.join(dirs.LOG, 'gunc', '{sample}.out'),
    conda:
        'gunc',
    threads: config['gunc_threads'],
    resources:
        mem_mb = config['gunc_mem_mb'],
    params:
        bin_dir = os.path.join(dirs.TMP, '{sample}'),
        out_dir = os.path.join(dirs.OUT, '2_gunc', '{sample}'),
        diamond_db = config['diamond_db'],
    shell:
        """
        mkdir -p {params.out_dir}
        gunc run --input_dir {params.bin_dir} --out_dir {params.out_dir} --db_file {params.diamond_db} --threads {threads} > {log} 2>&1
        """


rule gtdbtk:
    input:
        os.path.join(dirs.TMP, '{sample}.out'),
    output:
        os.path.join(dirs.OUT, '3_gtdbtk', '{sample}', 'report.tsv'),
    log:
        os.path.join(dirs.LOG, 'gtdbtk', '{sample}.out'),
    conda:
        'gtdbtk',
    threads: config['gtdbtk_threads'],
    resources:
        mem_mb = config['gtdbtk_mem_mb'],
    params:
        bin_dir = os.path.join(dirs.TMP, '{sample}'),
        out_dir = os.path.join(dirs.OUT, '3_gtdbtk', '{sample}'),
        gtdb_db = config['gtdb_db'],
        ext = 'fa',
    shell:
        """
        export GTDBTK_DATA_PATH={params.gtdb_db} 
        gtdbtk classify_wf --genome_dir {params.bin_dir} --out_dir {params.out_dir} -x {params.ext} \
            --cpus {threads} --pplacer_cpus 1 --force --skip_ani_screen > {log} 2>&1 || echo 'No MAGs were classified' >> {log} 2>&1
            # --force makes it complete even without proteins
        if [[ -f "{params.out_dir}/gtdbtk.bac120.summary.tsv" ]]; then
            cp {params.out_dir}/gtdbtk.bac120.summary.tsv {output}
        else
            touch {output}
        fi
        """


rule get_mag_refs:
    input:
        os.path.join(dirs.OUT, '3_gtdbtk', '{sample}', 'report.tsv'),
    output:
        os.path.join(dirs.OUT, '4_dnadiff', '{sample}', 'mag_refs.out'),
    params:
        out_dir = os.path.join(dirs.OUT, '4_dnadiff', '{sample}'),
        gtdb_db = config['gtdb_db'],
    run:
        if not os.path.isdir(params.out_dir): os.makedirs(params.out_dir)
        if os.path.getsize(str(input)) != 0:
            df = pd.read_csv(str(input), sep = '\t')
            df.apply(lambda row : pair_mag_refs(row, params.out_dir, params.gtdb_db), axis = 1)
        open(str(output), 'w').close()


rule dnadiff:
    input:
        fa = os.path.join(dirs.TMP, '{sample}', '{bin_num}.fa'),
        made_ref = os.path.join(dirs.OUT, '4_dnadiff', '{sample}', 'mag_refs.out'),
    output:
        ref = os.path.join(dirs.OUT, '4_dnadiff', '{sample}', '{bin_num}.ref.fa'),
        rep = os.path.join(dirs.OUT, '4_dnadiff', '{sample}', '{bin_num}' + '.report'),
    log:
        os.path.join(dirs.LOG, 'dnadiff', '{sample}.{bin_num}.out'), 
    conda:
        'mummer'
    params:
        prefix = os.path.join(dirs.OUT, '4_dnadiff', '{sample}', '{bin_num}'),
        ref = os.path.join(dirs.OUT, '4_dnadiff', '{sample}', '{bin_num}.ref'),
    shell:
        """
        if [[ -f "{params.ref}" ]]; then
            REF_PATH=`cat {params.ref}`
            if [[ -f "$REF_PATH" ]]; then
                zcat $REF_PATH > {output.ref}
                dnadiff {output.ref} {input.fa} -p {params.prefix} > {log}
            else
                touch {output.ref}
                touch {output.rep}
            fi
        else
            touch {output.ref}
            touch {output.rep}
        fi
        """


rule parse_dnadiff:
    input:
        os.path.join(dirs.OUT, '4_dnadiff', '{sample}', '{bin_num}' + '.report'),
    output:
        os.path.join(dirs.OUT, '4_dnadiff', '{sample}', '{bin_num}' + '.diff.tsv'),
    run:
        parse_dnadiff(str(input), str(output))


rule aggregate_dnadiff:
    input:
        lambda wildcards: expand(rules.parse_dnadiff.output, bin_num = get_bin_nums(wildcards.sample, dirs.TMP), sample = wildcards.sample),
    output:
        os.path.join(dirs.OUT, '4_dnadiff', '{sample}', 'report.tsv'),
    shell:
        """
        tmp=`echo -n "{input}" | wc -c`
        if [ $tmp -gt 0 ]; # Only if there are (refined) bins generated
        then
            cat {input}>{output}
        else
            touch {output}
        fi
        """


rule quast:
    input:
        fa = os.path.join(dirs.TMP,'{sample}','{bin_num}.fa'),
        ref = os.path.join(dirs.OUT,'4_dnadiff','{sample}','{bin_num}.ref.fa'),
    output:
        os.path.join(dirs.OUT, '5_quast', '{sample}', '{bin_num}', 'report.tsv'),
    log:
        os.path.join(dirs.LOG, 'quast', '{sample}.{bin_num}.out'), 
    conda:
        "quast"
        #os.path.join(config['env_yamls'], 'quast.yaml'),
    threads: config['quast_threads'],
    resources:
        mem_mb = config['quast_mem_mb'],
    params:
        out_dir = os.path.join(dirs.OUT, '5_quast', '{sample}', '{bin_num}'),
        min_len = config['min_contig_len'],
    shell:
        """
        if [[ -s {input.ref} ]]; then
            quast.py --threads {threads} -r {input.ref} -m {params.min_len} -o {params.out_dir} {input.fa} --no-plots
        else
            echo "MAG was not classified at the species level."
            touch {output}
        fi
        """


rule aggregate_quast:
    input:
        lambda wildcards: expand(rules.quast.output, bin_num = get_bin_nums(wildcards.sample, dirs.TMP), sample = wildcards.sample),
    output:
        os.path.join(dirs.OUT, '5_quast', '{sample}', 'report.csv'),
    run:
        aggregate_quast(input, str(output))



rule ctg_name_edit:
    input:
        os.path.join(dirs.TMP, '{sample}', '{bin_num}.fa'),
    output:
        os.path.join(dirs.OUT, '6_prokka', '{sample}', 'bins', '{bin_num}.fa'),
    params:
        out_dir = os.path.join(dirs.OUT, '6_prokka', '{sample}', 'bins'),
    shell:
        """
        mkdir -p {params.out_dir}
        if awk '/^>/ {{ if(length($0) > 37) exit 1 }}' {input}; then
            ln -s {input} {output}
        else
            awk '/^>/ {{ if(length($0) > 37) print substr($0, 1, 37); \
            else print }} !/^>/ {{ print }}' {input} > {output}
        fi
        """


rule prokka:
    input:
        os.path.join(dirs.OUT, '6_prokka', '{sample}', 'bins', '{bin_num}.fa'),
    output:
        os.path.join(dirs.OUT, '6_prokka', '{sample}', '{bin_num}.txt'),
        os.path.join(dirs.OUT, '6_prokka', '{sample}', '{bin_num}.tsv'),
    log:
        os.path.join(dirs.LOG, 'prokka', '{sample}.{bin_num}.out'), 
    conda:
        "prokka"
        #os.path.join(config['env_yamls'], 'prokka.yaml'),
    threads: config['prokka_threads'],
    params:
        out_dir = os.path.join(dirs.OUT, '6_prokka', '{sample}'),
        prefix = '{bin_num}',
    shell:
        """
        prokka {input} --kingdom Bacteria --outdir {params.out_dir} \
            --prefix {params.prefix} --locustag {params.prefix} \
            --force --cpus {threads} > {log} 2>&1
        if [ "{params.prefix}" = "0" ]; then
            for f in {params.out_dir}/PROKKA_*; do
                if [ -f "$f" ]; then
                    suffix="${{f##*.}}"
                    mv "$f" "{params.out_dir}/0.$suffix"
                fi
            done
        fi
        """


rule summarize_gene_cts:
    input:
        txt = os.path.join(dirs.OUT, '6_prokka', '{sample}', '{bin_num}.txt'),
        tsv = os.path.join(dirs.OUT, '6_prokka', '{sample}', '{bin_num}.tsv'),
    output:
        os.path.join(dirs.OUT, '6_prokka', '{sample}', '{bin_num}.csv'),
    params:
        summ_script = os.path.join(dirs_scr, 'summarize_gene_cts.py'),
        prefix = '{bin_num}',
    shell:
        """
        python {params.summ_script} {input.txt} {input.tsv} {params.prefix} {output}
        """ 


rule aggregate_gene_cts:
    input:
        lambda wildcards: expand(rules.summarize_gene_cts.output, bin_num = get_bin_nums(wildcards.sample, dirs.TMP), sample = wildcards.sample),
    output:
        os.path.join(dirs.OUT, '6_prokka', '{sample}', 'report.csv'),
    shell:
        """
        cat {input} > {output}
        """ 


rule summarize_reports:
    input:
        checkm2 = os.path.join(dirs.OUT, '0_checkm2', '{sample}', 'quality_report.tsv'),
        strain_het = os.path.join(dirs.OUT, '1_checkm1', 'strain_het', '{sample}', 'report.tsv'),
        mag_ra = os.path.join(dirs.OUT, '1_checkm1', 'mag_ra', '{sample}', 'report.csv'),
        gunc = os.path.join(dirs.OUT, '2_gunc', '{sample}', 'GUNC.progenomes_2.1.maxCSS_level.tsv'),
        gtdb = os.path.join(dirs.OUT, '3_gtdbtk', '{sample}', 'report.tsv'),
        diff = os.path.join(dirs.OUT, '4_dnadiff', '{sample}', 'report.tsv'),
        quast = os.path.join(dirs.OUT, '5_quast', '{sample}', 'report.csv'),
        gene_cts = os.path.join(dirs.OUT, '6_prokka', '{sample}', 'report.csv'),
    output:
        os.path.join(dirs.OUT, 'final_reports', '{sample}.summary.csv'),
    params:
        summ_script = os.path.join(dirs_scr, 'summarize_reports.py'),
    shell:
        """
        python {params.summ_script} {input.checkm2} {input.strain_het} {input.mag_ra} {input.gunc} {input.gtdb} {input.diff} {input.quast} {input.gene_cts} {output} 
        """



rule make_config:
    input:
        expand(os.path.join(dirs.OUT, 'final_reports', '{sample}.summary.csv'), sample = SAMPLES),
    output:
        os.path.join(dirs.OUT, 'final_reports', 'complete.txt'),
    params:
        out_dir = os.path.join(dirs.OUT, 'final_reports'),
    run:
        open(str(output), 'w').close()


